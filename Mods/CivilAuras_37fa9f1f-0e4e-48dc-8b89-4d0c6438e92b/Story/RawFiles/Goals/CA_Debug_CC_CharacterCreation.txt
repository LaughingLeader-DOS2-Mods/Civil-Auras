Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CharacterCreationLevels("SYS_Character_Creation_A");  //default SYS_Character_Creation_A
DB_GLO_FirstLevelAfterCharacterCreation("TestLevel");
// level name, start point trigger, name of movie to play (if any)
DB_CharacterCreationTransitionInfo("TestLevel",(TRIGGERGUID)TRIGGERGUID_TestLevel_StartPoint_fe2eec91-ca9d-4340-9be9-320cdc536134,"");    // TRIGGERGUID_StartPoint_000__000_0d073c19-2275-8ebd-3956-3eb369cdb1a7

DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin_7b6c1f26-fe4e-40bd-a5d0-e6ff58cef4fe);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin2_c451954c-73bf-46ce-a1d1-caa9bbdc3cfd);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin3_41a06985-7851-4c29-8a78-398ccb313f39);
DB_GenericOrigins((CHARACTERGUID)S_Player_GenericOrigin4_41a594ed-b768-4289-9f17-59f701cc6910);
KBSECTION
IF
DB_GenericOrigins(_Orig)
THEN
SetTag(_Orig,"GENERIC");

IF
DB_InCharacterCreation(1)
THEN
IterateUsers("_CCUserSetup");

IF
UserEvent(_User,"_CCUserSetup")
AND
GetUserProfileID(_User,_UserName)
AND
DB_CharacterCreationDummy(_Dummy)
AND
NOT DB_AssignedDummyForUser(_,_Dummy)
AND
NOT DB_AssignedDummyForUser(_UserName,_)
THEN
ProcAssignDummyToUser(_Dummy,_UserName);

IF
UserEvent(_User,"_CCUserSetup")
AND
GetUserProfileID(_User,_UserName)
AND
DB_AssignedDummyForUser(_UserName,_Dummy)
THEN
CharacterAssignToUser(_User,_Dummy);
MakePlayerActive(_Dummy);

// Support "loadlevel starting_level" directly from the main menu (have same effect as
// first going through character creation)
IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
AND
DB_GLO_FirstLevelAfterCharacterCreation(_FirstLevel)
THEN
DB_CharacterCreationLevels(_FirstLevel);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
AND
DB_CurrentLevel(_Lvl)
AND
DB_CharacterCreationLevels(_Lvl)
AND
DB_GLO_FirstLevelAfterCharacterCreation(_FirstLevel)
AND
DB_CharacterCreationTransitionInfo(_FirstLevel,_StartTrigger,_Movie)
AND
DB_IsPlayer(_Char)
AND
IsTagged(_Char,"AVATAR",1)
THEN
ProcMovePartyToStart(_Char,_StartTrigger,_Movie);

IF
RegionStarted(_Region)
AND
IsGameLevel(_Region,1)
THEN
DB_StartedActualGame(1);

IF
UserDisconnected(_UserID,_,_UserProfile)
AND
NOT DB_InCharacterCreation(1)
AND 
NOT DB_StartedActualGame(1)
AND
DB_SelectedCC(_Char,_UserProfile)
THEN
PROC_GLO_PartyMembers_Remove(_Char,1);
ClearTag(_Char,"AVATAR");
NOT DB_IsPlayer(_Char);
ProcUnRegisterPlayerTriggers(_Char);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
THEN
//force remove this
NOT DB_InCharacterCreation(1);
ProcCheckRemoveOtherOrigins();

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
THEN
NOT DB_AlreadyTeleported(1);

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
DB_TeleportedToStartCharacters(_Char)
AND
CharacterIsInPartyWith(_Char,_NewChar,1)
THEN
DB_AlreadyTeleported(1);

PROC
ProcMovePartyToStart((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
NOT DB_AlreadyTeleported(1)
THEN
DB_TeleportedToStartCharacters(_NewChar);
PROC_GLO_CharacterCreationTeleportWithOptionalMovie(_NewChar,_StartTrigger,_Movie);

PROC
PROC_GLO_CharacterCreationTeleportWithOptionalMovie((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
_Movie != ""
THEN
CharacterTeleportPartiesToTriggerWithMovie(_StartTrigger,"",_Movie);

PROC
PROC_GLO_CharacterCreationTeleportWithOptionalMovie((CHARACTERGUID)_NewChar,(TRIGGERGUID)_StartTrigger,(STRING)_Movie)
AND
_Movie == ""
THEN
CharacterTeleportPartiesToTrigger(_StartTrigger,"");

// This breaks creation in editor, comment it out during testing.
/*
IF
RegionStarted("TestLevel")
THEN
ProcCheckRemoveOtherOrigins();
*/

PROC
ProcCheckRemoveOtherOrigins()
AND
NOT DB_InCharacterCreation(1)
AND
QueryOnlyOnce("SetupNonSelectedOrigins")
THEN
ProcRemoveOtherOrigins();


/*
PROC
ProcRemoveOtherOrigins()
AND
DB_OriginRecruitmentLocation_Region("FJ_FortJoy_Main",(CHARACTERGUID)_Origin,_,_)
AND
IsTagged(_Origin,"AVATAR",0)
THEN
PROC_GLO_PartyMembers_Remove(_Origin,1);
DB_OriginNotSelected(_Origin);
*/

PROC
ProcRemoveOtherOrigins()
AND
DB_GenericOrigins(_GenericOrigin)
AND
IsTagged(_GenericOrigin,"AVATAR",0)
THEN
CharacterRemoveFromParty(_GenericOrigin);
CharacterMakeNPC(_GenericOrigin);
SetOnStage(_GenericOrigin,0);



//========================================//
//Ripped from Global_CharacterCreation.txt
//========================================//

IF
DB_CurrentGameMode(_Mode)
AND
_Mode != "Campaign"
THEN
GoalCompleted;

PROC
ProcRemovePreviousSelectedCharacter((STRING)_UserProfile)
AND
DB_SelectedCC((CHARACTERGUID)_Char,(STRING)_UserProfile)
THEN
ClearTag(_Char,"AVATAR");
NOT DB_IsPlayer(_Char);
ProcUnRegisterPlayerTriggers(_Char);
NOT DB_SelectedCC(_Char,_UserProfile);

PROC
ProcSetSelectedCharCreationPlayer((CHARACTERGUID)_Char,(STRING)_UserProfile)
THEN
ProcRemovePreviousSelectedCharacter(_UserProfile);
SetTag(_Char,"AVATAR");
DB_IsPlayer(_Char);
ProcRegisterPlayerTriggers(_Char);
DB_SelectedCC(_Char,_UserProfile);

IF
UserDisconnected(_UserID,_,_UserProfile)
AND
DB_InCharacterCreation(1)
THEN
ProcRemovePreviousSelectedCharacter(_UserProfile);

IF
CharacterSelectedInCharCreation(_Char,_UserID)
AND
GetUserProfileID(_UserID,_UserProfile)
THEN
ProcSetSelectedCharCreationPlayer(_Char,_UserProfile);

IF
CharacterSelectedInCharCreation(_Char,_UserID)
AND
CharacterHasTalent(_Char,"AnimalEmpathy",1)
THEN
SetTag(_Char,"PETPAL");

/*
IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_InCharacterCreation(1)
AND
DB_CurrentLevel(_Lvl)
AND
NOT DB_CharacterCreationLevels(_Lvl)
AND
QueryOnlyOnce("SetupNonSelectedOrigins")
AND
DB_OriginRecruitmentLocation((CHARACTERGUID)_Origin,_)
AND
IsTagged(_Origin,"AVATAR",0)
THEN
PROC_GLO_PartyMembers_Remove(_Origin,1);
DB_OriginNotSelected(_Origin);
*/

IF
UserConnected(_,_,_UserName)
AND
DB_InCharacterCreation(1)
AND
DB_CharacterCreationDummy(_Dummy)
AND
NOT DB_AssignedDummyForUser(_,_Dummy)
AND
NOT DB_AssignedDummyForUser(_UserName,_)
THEN
ProcAssignDummyToUser(_Dummy,_UserName);

PROC
ProcAssignDummyToUser((CHARACTERGUID)_Dummy,(STRING)_UserName)
THEN
CharacterMakePlayer(_Dummy, NULL_00000000-0000-0000-0000-000000000000);

PROC
ProcAssignDummyToUser((CHARACTERGUID)_Dummy,(STRING)_UserName)
AND
CharacterAddToCharacterCreation(_Dummy,0,1)
THEN
DB_AssignedDummyForUser(_UserName,_Dummy);

IF
UserConnected(_UserID,_,_UserName)
AND
DB_AssignedDummyForUser(_UserName,_Dummy)
THEN
CharacterAssignToUser(_UserID,_Dummy);
MakePlayerActive(_Dummy);

IF
RegionStarted(_)
AND
DB_ChosenOriginWaitingForTeleport(_Player)
THEN
CharacterPurgeQueue(_Player);

IF
DB_CurrentLevel(_Region)
AND
DB_DoTeleportChosenPlayer(1)
AND
DB_IsPlayer(_Player)
THEN
CharacterPurgeQueue(_Player);
TeleportTo(_Player,CHARACTERGUID_S_GLO_CharacterCreationDummy_001_da072fe7-fdd5-42ae-9139-8bd4b9fca406,"ChosenPlayerTeleported",1);
DB_ChosenOriginWaitingForTeleport(_Player);
MakePlayerActive(_Player);
NOT DB_DoTeleportChosenPlayer(1);

IF
StoryEvent((CHARACTERGUID)_Player,"ChosenPlayerTeleported")
AND
DB_ChosenOriginWaitingForTeleport(_Player)
THEN
NOT DB_ChosenOriginWaitingForTeleport(_Player);

IF
CharacterCreationFinished((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000)
AND
DB_CharacterCreationDummy(_Npc)
THEN
CharacterRemoveFromParty(_Npc);
CharacterMakeNPC(_Npc);
SetOnStage(_Npc,0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "_CA_Debug_CC_Start"
