Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_CA_CatBag((ITEMGUID)_Item, 0);
DB_CA_CatBag_Sounds_In(0, "Animals_Cat_ABC_Dodge_01");
DB_CA_CatBag_Sounds_In(1, "Animals_Cat_ABC_Talk_Scared");
DB_CA_CatBag_Sounds_In(2, "Animals_Cat_ABC_Spawn_02");
DB_CA_CatBag_Sounds_In(3, "Animals_Cat_ABC_Die_06");
DB_CA_CatBag_Sounds_In(4, "Animals_Cat_ABC_Die_07");

DB_CA_CatBag_Sounds_Out(0, "Animals_Cat_ABC_Sit_Talk_Normal_01");
DB_CA_CatBag_Sounds_Out(1, "Animals_Cat_ABC_Use_05");

//DB_CA_CatCharacter((CHARACTERGUID)S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830);
KBSECTION
//REGION TALENT_BOOKS
PROC
CA_TALENT_Unlock((CHARACTERGUID)_Player, (STRING)_Talent)
AND
CharacterHasTalent(_Player, _Talent, 0)
THEN
CharacterAddTalent(_Player, _Talent);

PROC
CA_TALENTBOOK_Unlock((CHARACTERGUID)_Player, (STRING)_Talent, (ITEMGUID)_Item)
AND
CharacterHasTalent(_Player, _Talent, 0)
THEN
CharacterAddTalent(_Player, _Talent);
ItemDestroy(_Item);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Talent_PetPal_d64f8314-0700-4d43-b854-9b2f73073945", (ITEMGUID)_Item)
AND
CharacterHasTalent(_Player, "AnimalEmpathy", _Val)
AND
DB_CA_Book_PetPal_Message(_Val, _Message)
AND
DB_CA_Book_Talent_Sound(_Val, _Sound)
THEN
CharacterStatusText(_Player, _Message);
CA_TALENTBOOK_Unlock(_Player, "AnimalEmpathy", _Item);
CharacterPlayHUDSound(_Player, _Sound);

//END_REGION

//REGION CAT_STUFF
PROC
CA_CatBag_PlaySound_In((CHARACTERGUID)_Player)
AND
Random(5, _Ran)
AND
DB_CA_CatBag_Sounds_In(_Ran, _Sound)
THEN
PlaySound(_Player, _Sound);

PROC
CA_CatBag_PlaySound_Out((CHARACTERGUID)_Player)
AND
Random(2, _Ran)
AND
DB_CA_CatBag_Sounds_Out(_Ran, _Sound)
THEN
PlaySound(_Player, _Sound);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 0)
THEN
CharacterStatusText(_Player, "I don't have a cat summon.");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 1)
THEN
CharacterRemoveSkill(_Player, "Summon_Cat");
ItemDestroy(_Item);
ItemTemplateAddTo("MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", _Player, 1);
CA_CatBag_PlaySound_In(_Player);


IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 1)
THEN
CharacterStatusText(_Player, "I already have a cat summon.");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 0)
THEN
CharacterAddSkill(_Player, "Summon_Cat");
ItemDestroy(_Item);
ItemTemplateAddTo("MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", _Player, 1);
CA_CatBag_PlaySound_Out(_Player);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
ObjectExists(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, 1)
THEN
DisplayText(_Player, "Here kitty, kitty...");
CA_CatBag_PlaySound_Out(_Player);
PlayAnimation(_Player, "use_loot");
TeleportTo(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, _Player);
PlayEffect(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, "RS3_FX_GP_Impacts_Dust_01");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
ObjectExists(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, 0)
THEN
CharacterStatusText(_Player, "You don't think the Black Cat is around...");

//END_REGION

//REGION BONUS_POINT_POTIONS
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_MaxSource_c747accc-bc67-4aa4-af24-5fd4e163aa87", (ITEMGUID)_Item)
AND
CharacterGetMaxSourcePoints(_Player, (INTEGER)_CurrentSP)
AND
IntegerSum(_CurrentSP, 1, (INTEGER)_MaxSP)
THEN
CharacterOverrideMaxSourcePoints(_Player, _MaxSP);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Talent_06ebb17f-babe-429c-91f3-11d537dad6ca", (ITEMGUID)_Item)
THEN
CharacterAddTalentPoint(_Player,1);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Ability_a3a2b79b-286a-4dda-8e6b-68c81b95ea63", (ITEMGUID)_Item)
THEN
CharacterAddAbilityPoint(_Player, 1);

/*
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Civil_ab30cfaf-8118-4718-9051-1966aa1ae054", (ITEMGUID)_Item)
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
*/

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Attribute_ca660d0e-05b6-4e43-a612-7f22e9fd2205", (ITEMGUID)_Item)
THEN
CharacterAddAttributePoint(_Player, 1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
