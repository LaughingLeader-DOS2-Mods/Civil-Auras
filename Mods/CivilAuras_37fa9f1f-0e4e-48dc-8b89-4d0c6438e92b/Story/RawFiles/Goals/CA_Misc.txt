Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Respec mirror talent fix
//DB_CA_Talent_Blacklist(_Talent, _Player);

//DB_CA_CatCharacter((CHARACTERGUID)S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830);

//DB_CA_SummonedCats((CHARACTERGUID)_Player, (CHARACTERGUID)_Cat)
//DB_CA_SummonedCats(_Player, _Cat)

CA_Misc_InitSettings();
KBSECTION
//REGION TALENT_BOOKS
PROC
CA_TALENT_PlayEffect((CHARACTERGUID)_Player, (STRING)_Talent)
AND
DB_CA_Talent_Effect(_Talent, _Effect)
AND
GetPosition(_Player, _X, _PY, _Z)
AND
RealSum(_PY, 1.0, _Y)
THEN
PlayEffectAtPosition(_Effect, _X, _Y, _Z);

PROC
CA_TALENT_Unlock((CHARACTERGUID)_Player, (STRING)_Talent)
AND
CharacterHasTalent(_Player, _Talent, 0)
THEN
CA_TALENT_PlayEffect(_Player, _Talent);
CharacterAddTalent(_Player, _Talent);

PROC
CA_TALENTBOOK_Unlock((CHARACTERGUID)_Player, (STRING)_Talent, (ITEMGUID)_Item)
AND
CharacterHasTalent(_Player, _Talent, 1)
AND
NOT DB_CA_Talent_Blacklist(_Talent, _Player)
THEN
DB_CA_Talent_Blacklist(_Talent, _Player);

PROC
CA_TALENTBOOK_Unlock((CHARACTERGUID)_Player, (STRING)_Talent, (ITEMGUID)_Item)
AND
CharacterHasTalent(_Player, _Talent, 0)
AND
NOT DB_CA_Talent_Blacklist(_Talent, _Player)
THEN
CA_TALENT_PlayEffect(_Player, _Talent);
CharacterAddTalent(_Player, _Talent);
ItemDestroy(_Item);
DB_CA_Talent_Blacklist(_Talent, _Player);

QRY
CA_Misc_Talentbook_PlayEffects((CHARACTERGUID)_Player, (STRING)_Talent, (INTEGER)_Index)
AND
DB_CA_Talentbook_Effects(_Index, _Talent, _Message, _Sound)
THEN
CharacterPlayHUDSound(_Player, _Sound);
CharacterStatusText(_Player, _Message);

QRY
CA_Misc_Talentbook_PlayEffects((CHARACTERGUID)_Player, (STRING)_Talent, (INTEGER)_Index)
AND
NOT DB_CA_Talentbook_Effects(_Index, _Talent, _, _)
THEN
DB_NOOP(1);

IF
CharacterUsedItemTemplate(_Player, _Template, _Item)
AND
DB_CA_TalentBook_Talent(_Template, _Talent)
AND
DB_CA_Talent_Blacklist(_Talent, _Player)
AND
CA_Misc_Talentbook_PlayEffects(_Player, _Talent, 1)
THEN
//DebugText(_Player, "[CivilAuras] Hey, no cheating!");
CharacterStatusText(_Player, "Talent was previously known. This book won't help you.");

IF
CharacterUsedItemTemplate(_Player, _Template, _Item)
AND
DB_CA_TalentBook_Talent(_Template, _Talent)
AND
NOT DB_CA_Talent_Blacklist(_Talent, _Player)
AND
CharacterHasTalent(_Player, _Talent, _Index)
AND
CA_Misc_Talentbook_PlayEffects(_Player, _Talent, _Index)
THEN
CA_TALENTBOOK_Unlock(_Player, _Talent, _Item);
//END_REGION

//REGION CAT_STUFF
PROC
CA_CatBag_PlaySound_In((CHARACTERGUID)_Player)
AND
Random(5, _Ran)
AND
DB_CA_CatBag_Sounds_In(_Ran, _Sound)
THEN
PlaySound(_Player, _Sound);

PROC
CA_CatBag_PlaySound_Out((CHARACTERGUID)_Player)
AND
Random(2, _Ran)
AND
DB_CA_CatBag_Sounds_Out(_Ran, _Sound)
THEN
PlaySound(_Player, _Sound);

PROC
CA_RemoveCatSummon((CHARACTERGUID)_Player)
AND
DB_CA_SummonedCats(_Player, (CHARACTERGUID)_Cat)
AND
ObjectExists(_Cat, 1)
THEN
CharacterDieImmediate(_Cat, 0, "None");
NOT DB_CA_SummonedCats(_Player, _Cat);

QRY
CA_Misc_CatIsGone()
AND
ObjectExists(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, 0)
THEN
DB_NOOP(1);

QRY
CA_Misc_CatIsGone()
AND
DB_IsPlayer(_Player)
AND
DB_CA_SummonedCats(_Player, _Cat)
AND
ObjectExists(_Cat, 0)
THEN
DB_NOOP(1);

QRY
CA_Misc_CatIsGone()
AND
DB_IsPlayer(_Player)
AND
NOT DB_CA_SummonedCats(_Player, _)
THEN
DB_NOOP(1);

PROC
CA_Misc_ClearCat((CHARACTERGUID)_Player)
AND
DB_CA_SummonedCats(_Player, _Cat)
THEN
NOT DB_CA_SummonedCats(_Player, _Cat);

PROC
CA_TeleportCat((CHARACTERGUID)_Player, (CHARACTERGUID)_Cat)
THEN
DisplayText(_Player, "Here kitty, kitty...");
CA_CatBag_PlaySound_Out(_Player);
PlayAnimation(_Player, "use_loot");
TeleportTo(_Cat, _Player);
PlayEffect(_Cat, "RS3_FX_GP_Impacts_Dust_01");
PlaySound(_Cat, "Skill_Earth_ThrowDust_Projectile");

IF
SkillCast(_Player, "Summon_Cat", _)
AND
DB_IsPlayer((CHARACTERGUID)_Cat)
AND
CharacterIsSummon(_Cat, 1)
AND
GetTemplate(_Cat, _Template)
AND
_Template == "Summons_Cat_4f7cdf30-0d44-44d2-bcf2-91850728107d"
THEN
CA_Misc_ClearCat(_Player);
DB_CA_SummonedCats(_Player, _Cat);


IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 0)
THEN
CharacterStatusText(_Player, "I don't have a cat to summon.");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 1)
THEN
CharacterRemoveSkill(_Player, "Summon_Cat");
ItemDestroy(_Item);
ItemTemplateAddTo("MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", _Player, 1);
CA_CatBag_PlaySound_In(_Player);
CA_RemoveCatSummon(_Player);
//CharacterRemoveSummons(_Player, 0);
PlayAnimation(_Player, "use_activate");


IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 1)
THEN
CharacterStatusText(_Player, "I already have a cat.");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatSack_Full_c79ac316-5ae6-4631-b88c-f52052e1d3b8", (ITEMGUID)_Item)
AND
CharacterHasSkill(_Player, "Summon_Cat", 0)
THEN
CharacterAddSkill(_Player, "Summon_Cat");
ItemDestroy(_Item);
ItemTemplateAddTo("MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", _Player, 1);
CA_CatBag_PlaySound_Out(_Player);
PlayAnimation(_Player, "emotion_normal");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
CharacterIsInCombat(_Player, 0)
AND
ObjectExists(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, 1)
THEN
CA_TeleportCat(_Player, S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
CharacterIsInCombat(_Player, 0)
AND
DB_CA_SummonedCats(_Player, (CHARACTERGUID)_Cat)
AND
ObjectExists(_Cat, 1)
THEN
CA_TeleportCat(_Player, _Cat);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
CharacterIsInCombat(_Player, 1)
AND
ObjectExists(S_FTJ_BlackCat_4bff6ccc-5889-4c90-bcb1-572f1c29d830, 1)
THEN
CharacterStatusText(_Player, "This is no time to be playing with fish!");

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", (ITEMGUID)_Item)
AND
CA_Misc_CatIsGone()
THEN
CharacterStatusText(_Player, "I don't think the Black Cat is around...");

//END_REGION

//REGION BONUS_POINT_POTIONS
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_MaxSource_c747accc-bc67-4aa4-af24-5fd4e163aa87", (ITEMGUID)_Item)
AND
CharacterGetMaxSourcePoints(_Player, (INTEGER)_CurrentSP)
AND
IntegerSum(_CurrentSP, 1, (INTEGER)_MaxSP)
THEN
CharacterOverrideMaxSourcePoints(_Player, _MaxSP);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Talent_06ebb17f-babe-429c-91f3-11d537dad6ca", (ITEMGUID)_Item)
THEN
CharacterAddTalentPoint(_Player,1);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Ability_a3a2b79b-286a-4dda-8e6b-68c81b95ea63", (ITEMGUID)_Item)
THEN
CharacterAddAbilityPoint(_Player, 1);

/*
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Civil_ab30cfaf-8118-4718-9051-1966aa1ae054", (ITEMGUID)_Item)
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
*/

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "CON_CA_Potion_Attribute_ca660d0e-05b6-4e43-a612-7f22e9fd2205", (ITEMGUID)_Item)
THEN
CharacterAddAttributePoint(_Player, 1);
//END_REGION

//REGION MISC
//Respec mirror expoit prevention
IF
RegionStarted(_Region)
AND
DB_CA_TalentBook_Talent(_Book, _Talent)
AND
DB_IsPlayer(_Player)
AND
NOT DB_CA_Talent_Blacklist(_Talent, _Player)
AND
CharacterHasTalent(_Player, _Talent, 1)
THEN
DB_CA_Talent_Blacklist(_Talent, _Player);
//END_REGION

//REGION SETTINGS
PROC
CA_Misc_InitSettings()
THEN
SysClear("DB_CA_TalentBook_Talent", 2);
DB_CA_TalentBook_Talent("BOOK_CA_Talent_PetPal_d64f8314-0700-4d43-b854-9b2f73073945", "AnimalEmpathy");

SysClear("DB_CA_Talent_Effect", 2);
DB_CA_Talentbook_Effects(0, "AnimalEmpathy", "Pet Pal Learned", "Items_Inventory_Book_Skillbook_Use");
DB_CA_Talentbook_Effects(1, "AnimalEmpathy", "Pet Pal Already Learned", "Items_Objects_Destroy");

DB_CA_Talent_Effect("AnimalEmpathy", "CA_FX_PetPal_Learned_01");

SysClear("DB_CA_CatBag_Sounds_In", 2);
DB_CA_CatBag_Sounds_In(0, "Animals_Cat_ABC_Dodge_01");
DB_CA_CatBag_Sounds_In(1, "Animals_Cat_ABC_Talk_Scared");
DB_CA_CatBag_Sounds_In(2, "Animals_Cat_ABC_Spawn_02");
DB_CA_CatBag_Sounds_In(3, "Animals_Cat_ABC_Die_06");
DB_CA_CatBag_Sounds_In(4, "Animals_Cat_ABC_Die_07");

SysClear("DB_CA_CatBag_Sounds_Out", 2);
DB_CA_CatBag_Sounds_Out(0, "Animals_Cat_ABC_Sit_Talk_Normal_01");
DB_CA_CatBag_Sounds_Out(1, "Animals_Cat_ABC_Use_05");
//END_REGION

//REGION DATABASE_REFRESH
PROC
CA_Misc_ResetDatabases()
THEN
SysClear("DB_CA_TalentBook_Talent", 2);
SysClear("DB_CA_Talentbook_Effects", 4);
SysClear("DB_CA_Talent_Effect", 2);
SysClear("DB_CA_CatBag_Sounds_In", 2);
SysClear("DB_CA_CatBag_Sounds_Out", 2);

PROC
CA_System_ClearDatabases()
THEN
CA_Misc_ResetDatabases();

PROC
CA_System_UpdateAll()
THEN
CA_Misc_InitSettings();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_CivilAuras"
