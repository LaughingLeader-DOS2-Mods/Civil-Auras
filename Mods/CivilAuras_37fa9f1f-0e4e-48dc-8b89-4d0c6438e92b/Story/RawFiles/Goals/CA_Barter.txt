Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_CA_BarterBonus((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
DB_CA_HighestBarter(0);

DB_CA_BarterParty_Message(0, "Your party's reputation proceeds you. Enjoy a discount.");
DB_CA_BarterParty_Message(1, "Hello. I know of your party. Discounts for all.");
DB_CA_BarterParty_Message(2, "Welcome. Discounts for all.");
KBSECTION
//REGION QUERIES_PROCS

QRY
CA_GetHighestBarter()
AND
DB_IsPlayer(_Player)
AND
CharacterGetAbility(_Player, "Barter", _BarterVal)
AND
_BarterVal > 0
AND
DB_CA_HighestBarter(_HighestBarter)
AND
_BarterVal > _HighestBarter
THEN
CA_ResetHighestBarter();
DB_CA_HighestBarter(_BarterVal);

QRY
CA_GetBarterBonus((CHARACTERGUID)_Target, (INTEGER)_TargetVal)
AND
DB_CA_HighestBarter(_HighestBarter)
AND
_HighestBarter > _TargetVal
AND 
IntegerSubtract(_HighestBarter, _TargetVal, _FinalVal)
THEN
DB_CA_BarterBonus(_Target, _FinalVal);

PROC
CA_ClearBonuses((CHARACTERGUID)_Target)
AND
DB_CA_BarterBonus(_Target, _Val)
THEN
NOT DB_CA_BarterBonus(_Target, _Val);

PROC
CA_ResetHighestBarter()
AND
DB_CA_HighestBarter(_Val)
THEN
NOT DB_CA_HighestBarter(_Val);
DB_CA_HighestBarter(0);
//END_REGION

//REGION MULTIPLAYER
IF
RequestTrade((CHARACTERGUID)_Target, (CHARACTERGUID)_Trader)
AND
DB_IsPlayer(_Target)
AND
DB_UserCounter(_Users)
AND
_Users > 0
AND
CA_GetHighestBarter(_Target)
AND
CharacterGetAbility(_Target, "Barter", (INTEGER)_TargetVal)
AND
CA_GetBarterBonus(_Target, _TargetVal)
AND
DB_CA_BarterBonus(_Target, _BarterBonus)
THEN
CharacterAddAbility(_Target, "Barter", _BarterBonus);

IF
TradeEnds((CHARACTERGUID)_Target, (CHARACTERGUID)_Trader)
AND
DB_UserCounter(_Users)
AND
_Users > 0
AND
DB_CA_BarterBonus(_Target, _BarterBonus)
THEN
CharacterRemoveAbility(_Target, "Barter", _BarterBonus);
CA_ClearBonuses(_Target);

//END_REGION

//REGION SINGLEPLAYER
IF
RequestTrade((CHARACTERGUID)_Target, (CHARACTERGUID)_Trader)
AND
DB_UserCounter(_Users)
AND
_Users <= 0
AND
CA_GetHighestBarter()
AND
DB_IsPlayer(_Player)
AND
CharacterGetAbility(_Player, "Barter", (INTEGER)_PlayerVal)
AND
CA_GetBarterBonus(_Player, _PlayerVal)
AND
DB_CA_BarterBonus(_Player, _BarterBonus)
THEN
CharacterAddAbility(_Player, "Barter", _BarterBonus);

IF
TradeEnds((CHARACTERGUID)_Target, (CHARACTERGUID)_Trader)
AND
DB_UserCounter(_Users)
AND
_Users <= 0
AND
DB_CA_BarterBonus(_Player, _BarterBonus)
THEN
CharacterRemoveAbility(_Player, "Barter", _BarterBonus);
NOT DB_CA_BarterBonus(_Player, _BarterBonus);
//END_REGION

//REGION BARTER_BONUS_TRADER_MESSAGES
IF
RequestTrade((CHARACTERGUID)_Target, (CHARACTERGUID)_Trader)
AND
DB_IsPlayer(_Target)
AND
DB_CA_BarterBonus(_Target, _BarterBonus)
AND
_BarterBonus > 0
AND
Random(2, _Ran)
AND
IntegerMin(_Ran, 2, _MessageNum)
AND
DB_CA_BarterParty_Message(_MessageNum, _Message)
THEN
DisplayText(_Trader, _Message);

//END_REGION

/*
//REGION DEBUG
IF
DialogStarted((STRING)_Dialog, (INTEGER)_ID)
AND
DialogGetInvolvedPlayer(_ID, 1, (CHARACTERGUID)_Player)
AND
DialogGetInvolvedNPC(_ID, 1, (CHARACTERGUID)_Trader)
AND
DB_IsPlayer(_Player)
AND
DB_UserCounter(_Users)
AND
IntegertoString(_Users, _StrUsers)
AND
StringConcatenate("You're playing with ", _StrUsers, _Msg1)
AND
StringConcatenate(_Msg1, " users", _Msg2)
THEN
CharacterStatusText(_Trader, _Msg2);

//END_REGION
*/
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "_CivilAuras"
