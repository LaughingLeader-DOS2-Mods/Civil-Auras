Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION FLAG_EVENTS
IF
ObjectFlagSet("CA_ADD_FIXER", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_ADD_FIXER", _Instance);
ItemTemplateAddTo("CON_CA_Potion_SkillFixer_3434d417-507a-4910-8f22-069050f7ad18", _Player, 1);

PROC
CA_Settings_Clear_BarterMessageSettings((CHARACTERGUID)_Player)
AND
DB_CA_Settings_Barter_Messages(_Player, _Val1, _Val2, _Val3)
THEN
NOT DB_CA_Settings_Barter_Messages(_Player, _Val1, _Val2, _Val3);

IF
ObjectFlagSet("CA_BARTERMESSAGES", (CHARACTERGUID)_Player, _Instance)
THEN
CA_Settings_Clear_BarterMessageSettings(_Player);
DB_CA_Settings_Barter_Messages(_Player, 1, 101, 99);

IF
ObjectFlagCleared("CA_BARTERMESSAGES", (CHARACTERGUID)_Player, _Instance)
THEN
CA_Settings_Clear_BarterMessageSettings(_Player);

IF
GlobalFlagSet("CA_DEBUG_REFRESHVENDORS")
THEN
GlobalClearFlag("CA_DEBUG_REFRESHVENDORS");
CA_TradeTreasure_ResetDynamicGeneration();

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
	CharacterHasSkill(_Player, "Target_CA_PepTalk", 1)
THEN
	CharacterRemoveSkill(_Player, "Target_CA_PepTalk");
	CharacterAddSkill(_Player, "Target_CA_PepTalk");
	CharacterStatusText(_Player, "Pep Talk Refreshed");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
	CharacterHasSkill(_Player, "Shout_CA_LuckyToggle", 1)
THEN
	CA_Luck_TurnAuraOff(_Player);
	CharacterRemoveSkill(_Player, "Shout_CA_LuckyToggle");
	CharacterAddSkill(_Player, "Shout_CA_LuckyToggle");
	CharacterStatusText(_Player, "Lucky Aura Refreshed");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
	CharacterHasSkill(_Player, "Shout_CA_StealthToggle", 1)
THEN
	RemoveStatus(_Player, "CA_STEALTH_AURA");
	CharacterRemoveSkill(_Player, "Shout_CA_StealthToggle");
	CharacterAddSkill(_Player, "Shout_CA_StealthToggle");
	CharacterStatusText(_Player, "Stealth Aura Refreshed");

IF
ObjectFlagSet("CA_DEBUG_FIXSKILLS", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_DEBUG_FIXSKILLS");
CA_Settings_FixSkills(_Player);

IF
ObjectFlagSet("CA_DEBUG_ADDBOOKS", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_DEBUG_ADDBOOKS");
ItemTemplateAddTo("BOOK_Skill_CA_LuckyAura_3413ffc6-ca7a-4e52-917b-b22910db8198", _Player, 1);
ItemTemplateAddTo("BOOK_Skill_CA_StealthAura_a1cc5702-b8e9-4b5e-9eeb-68776bd359ff", _Player, 1);
ItemTemplateAddTo("BOOK_Skill_CA_PepTalk_3a57d09c-fbc1-477d-b5df-7be5301487be", _Player, 1);
//ItemTemplateAddTo("MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", _Player, 1);
//ItemTemplateAddTo("MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", _Player, 1);

//END_REGION

//REGION CONFIG_BOOK
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
DB_IsPlayer(_Player)
AND
CharacterIsInCombat(_Player,0)
THEN
CA_Settings_InitSettings(_Player);
Proc_StartDialog(0, "CA_Dialog_Config", _Player, _Item);

//END_REGION

//REGION AUTO_ADD
IF
SavegameLoaded(_,_,_,_)
AND
DB_IsPlayer(_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", 0, 0)
AND
ItemTemplateIsInCharacterInventory(_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", 0)
THEN
ItemTemplateAddTo("BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", _Player, 1);

//END_REGION

//REGION DATABASE_REFRESH
PROC
CA_Settings_InitSettings((CHARACTERGUID)_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", 0, 0)
AND
ItemTemplateIsInCharacterInventory(_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", 0)
THEN
ItemTemplateAddTo("BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", _Player, 1);

IF
ObjectFlagSet("CA_DEBUG_REFRESHDB", (CHARACTERGUID)_Player, _Instance)
THEN
SetStoryEvent(_Player, "CA_Refresh");
//ObjectClearFlag(_Player, "CA_DEBUG_REFRESHDB");

IF
RegionStarted("TUT_Tutorial_A")
AND
DB_IsPlayer(_Player)
THEN
ObjectSetFlag(_Player, "CA_AUTOSNEAK");
CA_Settings_InitSettings(_Player);

IF
StoryEvent(_Object, "CA_Refresh")
AND
DB_IsPlayer(_Player)
THEN
CA_Settings_InitSettings(_Player);

//END_REGION

//REGION DEBUG
QRY
CA_QRY_DEBUG_ModFlags((STRING)_FlagName)
AND
_FlagName == "CA_AUTOSNEAK"
THEN
DB_NOOP(1);

QRY
CA_QRY_DEBUG_ModFlags((STRING)_FlagName)
AND
_FlagName == "CA_BARTER_BLACKLIST"
THEN
DB_NOOP(1);

/*
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
ObjectGetFlag(_Player, "CA_AUTOSNEAK", _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, "CA_AUTOSNEAK", _Val);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
ObjectGetFlag(_Player, "CA_BARTER_BLACKLIST", _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, "CA_BARTER_BLACKLIST", _Val);
*/

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
