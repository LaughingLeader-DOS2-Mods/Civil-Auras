Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION FLAG_EVENTS
IF
ObjectFlagSet("CA_ADD_FIXER", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_ADD_FIXER", _Instance);
ItemTemplateAddTo("CON_CA_Potion_SkillFixer_3434d417-507a-4910-8f22-069050f7ad18", _Player, 1);

PROC
CA_Settings_Clear_BarterMessageSettings((CHARACTERGUID)_Player)
AND
DB_CA_Settings_Barter_Messages(_Player, _Val1, _Val2, _Val3)
THEN
NOT DB_CA_Settings_Barter_Messages(_Player, _Val1, _Val2, _Val3);

IF
ObjectFlagSet("CA_BARTERMESSAGES", (CHARACTERGUID)_Player, _Instance)
THEN
CA_Settings_Clear_BarterMessageSettings(_Player);
DB_CA_Settings_Barter_Messages(_Player, 1, 101, 99);

IF
ObjectFlagCleared("CA_BARTERMESSAGES", (CHARACTERGUID)_Player, _Instance)
THEN
CA_Settings_Clear_BarterMessageSettings(_Player);

IF
GlobalFlagSet("CA_DEBUG_REFRESHVENDORS")
THEN
GlobalClearFlag("CA_DEBUG_REFRESHVENDORS");
CA_TradeTreasure_ResetDynamicGeneration();
CA_Debug_LogAll("Vendors refreshed.");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Target_CA_PepTalk", 1)
THEN
CharacterRemoveSkill(_Player, "Target_CA_PepTalk");
CharacterAddSkill(_Player, "Target_CA_PepTalk");
CharacterStatusText(_Player, "Pep Talk Refreshed");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Shout_CA_PersuasionToggle", 1)
THEN
CA_Luck_TurnAuraOff(_Player);
CharacterRemoveSkill(_Player, "Shout_CA_PersuasionToggle");
CharacterAddSkill(_Player, "Shout_CA_PersuasionToggle");
CharacterStatusText(_Player, "Persuasion Aura Refreshed");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Shout_CA_LuckyToggle", 1)
THEN
CA_Luck_TurnAuraOff(_Player);
CharacterRemoveSkill(_Player, "Shout_CA_LuckyToggle");
CharacterAddSkill(_Player, "Shout_CA_LuckyToggle");
CharacterStatusText(_Player, "Lucky Aura Refreshed");

PROC
CA_Settings_FixSkills((CHARACTERGUID)_Player)
AND
CharacterHasSkill(_Player, "Shout_CA_StealthToggle", 1)
THEN
RemoveStatus(_Player, "CA_STEALTH_AURA");
CharacterRemoveSkill(_Player, "Shout_CA_StealthToggle");
CharacterAddSkill(_Player, "Shout_CA_StealthToggle");
CharacterStatusText(_Player, "Stealth Aura Refreshed");

IF
ObjectFlagSet("CA_DEBUG_FIXSKILLS", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_DEBUG_FIXSKILLS");
CA_Settings_FixSkills(_Player);
CA_Debug_Log(_Player, "Fixed CA skills.");

IF
ObjectFlagSet("CA_DEBUG_ADDBOOKS", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_DEBUG_ADDBOOKS");
ItemTemplateAddTo("BOOK_CA_Skill_LuckyAura_Debug_ad983c5f-8b04-412d-a31e-b26e0dd8f849", _Player, 1);
ItemTemplateAddTo("BOOK_CA_Skill_StealthAura_Debug_3127cb67-3f22-4958-b1ab-a88653ea6c6f", _Player, 1);
ItemTemplateAddTo("BOOK_CA_Skill_PepTalk_Debug_1e0b4efd-0702-474a-be64-6762e26e26ef", _Player, 1);
ItemTemplateAddTo("BOOK_CA_Skill_PersuasionAura_Debug_915d1de7-5379-4472-994b-52b6849a4407", _Player, 1);
CA_Debug_Log(_Player, "Added all CA skillbooks.");
//ItemTemplateAddTo("MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", _Player, 1);
//ItemTemplateAddTo("MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", _Player, 1);

PROC
CA_Settings_RemoveAuraStatuses((CHARACTERGUID)_UserPlayer)
AND
DB_IsPlayer(_Player)
AND
CA_Global_UserMatch(_UserPlayer, _Player)
THEN
CA_Debug_RemoveAuras(_Player);
CA_Debug_RemoveBonuses(_Player);

PROC
CA_Settings_RemoveAllAuraStatuses()
THEN
IterateParties("CA_Events_RemoveAuraStatuses");

IF
StoryEvent((CHARACTERGUID)_Character, "CA_Events_RemoveAuraStatuses")
THEN
CA_Debug_RemoveAuras(_Character);
CA_Debug_RemoveBonuses(_Character);
CharacterStatusText(_Character, "<font color='#DC143C'>All Auras Removed</font>");

IF
ObjectFlagSet("CA_DEBUG_REMOVEAURASTATUSES", (CHARACTERGUID)_Player, _Instance)
THEN
ObjectClearFlag(_Player, "CA_DEBUG_REMOVEAURASTATUSES");
//CA_Settings_RemoveAuraStatuses(_Player);
CA_Settings_RemoveAllAuraStatuses();
CA_Aura_ResetAuraDatabases();
//END_REGION

//REGION CONFIG_BOOK
PROC
CA_Settings_UpdateVersionText()
AND
DB_Mods_Registered("CivilAuras", "LaughingLeader", _Version)
AND
StringConcatenate("Version: ", _Version, _VersionStr)
THEN
DialogSetVariableString("CA_Dialog_Config", "CA_ModVersion_ac1cbbcc-5732-4d45-b33d-8270f79b4521", _VersionStr);
//LeaderLog_Log("DEBUG","[CivilAuras:Settings] Set dialog variable to ", _VersionStr);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
CharacterIsPlayer(_Player, 1)
AND
CharacterIsInCombat(_Player,0)
THEN
CA_Settings_MarkHost(_Player);
Proc_StartDialog(0, "CA_Dialog_Config", _Item, _Player);
//END_REGION

//REGION AUTO_ADD
PROC
CA_Settings_AddSettingsBook((CHARACTERGUID)_Player)
AND
ItemTemplateIsInUserInventory(_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", 0, 0)
THEN
ItemTemplateAddTo("BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", _Player, 1);

IF
GameStarted(_,_)
THEN
TimerLaunch("LLCA_Timers_AddBookTimer", 2000);

IF
StoryEvent(_, "LeaderLib_Initialized")
THEN
TimerCancel("LLCA_Timers_AddBookTimer");

IF
TimerFinished("LLCA_Timers_AddBookTimer")
AND
NOT GlobalGetFlag("LeaderLib_Active", 1)
AND
DB_IsPlayer(_Player)
THEN
CA_Settings_AddSettingsBook(_Player);
//END_REGION

//REGION HOST_FLAGGING
IF
GameStarted(_,_)
AND
GlobalGetFlag("CA_HOST_TIMER_ACTIVE", 0)
THEN
GlobalSetFlag("CA_HOST_TIMER_ACTIVE");
TimerLaunch("CA_Timers_MarkPlayerHost", 25);

IF
TimerFinished("CA_Timers_MarkPlayerHost")
THEN
GlobalClearFlag("CA_HOST_TIMER_ACTIVE");
IterateParties("CA_Events_CheckHost");

IF
StoryEvent((CHARACTERGUID)_Player, "CA_Events_CheckHost")
THEN
CA_Settings_MarkHost(_Player);

PROC
CA_Settings_MarkHost((CHARACTERGUID)_Player)
AND
CharacterGetHostCharacter(_Player)
THEN
UserSetFlag(_Player, "CA_USER_ISHOST");
CA_Debug_Record(_Player, "Marked as host character");

PROC
CA_Settings_MarkHost((CHARACTERGUID)_Player)
AND
NOT CharacterGetHostCharacter(_Player)
AND
UserGetFlag(_Player, "CA_USER_ISHOST", 1)
THEN
UserClearFlag(_Player, "CA_USER_ISHOST", 0);
CA_Debug_Record(_Player, "Removed host flag");
//END_REGION

//REGION DATABASE_REFRESH
IF
ObjectFlagSet("CA_DEBUG_REFRESHDB", (CHARACTERGUID)_Player, _Instance)
THEN
CA_Debug_Record(_Player, "Manually refreshing databases.");
CA_System_UpdateAll();
ObjectClearFlag(_Player, "CA_DEBUG_REFRESHDB");
//END_REGION

//REGION DEBUG
QRY
CA_QRY_DEBUG_ModFlags((STRING)_FlagName)
AND
_FlagName == "CA_AUTOSNEAK_DISABLED"
THEN
DB_NOOP(1);

QRY
CA_QRY_DEBUG_ModFlags((STRING)_FlagName)
AND
_FlagName == "CA_BARTER_BLACKLIST"
THEN
DB_NOOP(1);

/*
IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
ObjectGetFlag(_Player, "CA_AUTOSNEAK_DISABLED", _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, "CA_AUTOSNEAK_DISABLED", _Val);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
ObjectGetFlag(_Player, "CA_BARTER_BLACKLIST", _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, "CA_BARTER_BLACKLIST", _Val);
*/

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_CivilAuras"
