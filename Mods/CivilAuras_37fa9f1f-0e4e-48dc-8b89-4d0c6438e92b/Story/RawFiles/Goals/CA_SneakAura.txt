Version 1
SubGoalCombiner SGC_AND
INITSECTION
//PlayerID, Bonus Value
//DB_CA_SneakBonus((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
//DB_CA_SneakAuraActive((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);

//PROCs
//CA_ClearSneakActive(_Player);CA_ClearSneakBonus(_Player);
KBSECTION
//REGION PROCS
PROC
CA_ClearSneakActive((CHARACTERGUID)_Player)
AND
DB_CA_SneakAuraActive(_Player, _Val)
THEN
NOT DB_CA_SneakAuraActive(_Player, _Val);

PROC
CA_ClearSneakBonus((CHARACTERGUID)_Player)
AND
DB_CA_SneakBonus(_Player, _Val)
THEN
NOT DB_CA_SneakBonus(_Player, _Val);

PROC
CA_StealthAuraEffect((CHARACTERGUID)_Player)
THEN
PlayEffect(_Player, "RS3_FX_Skills_Voodoo_Cast_Aoe_Air_Root_01_Texkey");
PlaySound(_Player, "Skill_Air_SmokeCover_Impact");

PROC
CA_StealthyEffect((CHARACTERGUID)_Player)
THEN
PlayEffect(_Player, "RS3_FX_Skills_Fire_Flamebreath_SubEmit_Smoke_01");
PlaySound(_Player, "Proj_Gren_Smoke_Projectile");

//END_REGION

//REGION SKILL_USE

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_StealthToggle", _)
AND
HasActiveStatus(_Player, "SNEAKING", 0)
AND
HasActiveStatus(_Player, "CA_STEALTH_AURA", 0)
THEN
/*ApplyStatus(_Player, "CA_STEALTH_AURA", -1.0);
ApplyStatus(_Player, "SNEAKING", -1.0);
CA_ClearSneakActive(_Player);
DB_CA_SneakAuraActive(_Player, 1);
CharacterStatusText(_Player, "Stealth aura enabled.");
CA_StealthAuraEffect(_Player);*/
CharacterStatusText(_Player, "Stealth Aura can only be used while Sneaking");

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_StealthToggle", _)
AND
HasActiveStatus(_Player, "SNEAKING", 1)
AND
HasActiveStatus(_Player, "CA_STEALTH_AURA", 0)
THEN
ApplyStatus(_Player, "CA_STEALTH_AURA", -1.0);
CA_ClearSneakActive(_Player);
DB_CA_SneakAuraActive(_Player, 1);
//CharacterStatusText(_Player, "Stealth Aura Enabled");
CA_StealthAuraEffect(_Player);

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_StealthToggle", _)
AND
HasActiveStatus(_Player, "CA_STEALTH_AURA", 1)
THEN
RemoveStatus(_Player, "CA_STEALTH_AURA");
CA_ClearSneakActive(_Player);
CharacterStatusText(_Player, "Stealth Aura Disabled");

//END_REGION

//REGION AUTO_TOGGLE

IF
CharacterStatusApplied((CHARACTERGUID)_Player, "SNEAKING", _)
AND
DB_CA_SneakAuraActive(_Player, 1)
AND
HasActiveStatus(_Player, "CA_STEALTH_AURA", 0)
THEN
ApplyStatus(_Player, "CA_STEALTH_AURA", -1.0);
CA_StealthAuraEffect(_Player);
CharacterStatusText(_Player, "Stealth Aura Auto-Enabled");

IF
SavegameLoaded(_,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_CA_SneakAuraActive(_Player, 1)
AND
HasActiveStatus(_Player, "SNEAKING", 1)
AND
HasActiveStatus(_Player, "CA_STEALTH_AURA", 0)
THEN
ApplyStatus(_Player, "CA_STEALTH_AURA", -1.0);
CA_StealthAuraEffect(_Player);
CharacterStatusText(_Player, "Stealth Aura Auto-Enabled");
//END_REGION

//REGION SNEAK_ALLY_BONUSES
IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_STEALTHY", (CHARACTERGUID)_AuraHost)
AND
DB_IsPlayer(_Player)
AND
CharacterGetAbility(_Player, "Sneaking", _PlayerSneak)
AND
CharacterGetAbility(_AuraHost, "Sneaking", _TargetSneak)
AND
_PlayerSneak < _TargetSneak
AND
IntegerSubtract(_TargetSneak, _PlayerSneak, _Bonus)
THEN
CharacterAddAbility(_Player, "Sneaking", _Bonus);
CA_ClearSneakBonus(_Player);
DB_CA_SneakBonus(_Player, _Bonus);
CA_StealthyEffect(_Player);

IF
CharacterStatusRemoved((CHARACTERGUID)_Player, "CA_STEALTHY", (CHARACTERGUID)_AuraHost)
AND
DB_CA_SneakBonus(_Player, _Bonus)
AND
_Bonus > 0
THEN
CharacterRemoveAbility(_Player, "Sneaking", _Bonus);
CA_ClearSneakBonus(_Player);
//END_REGION

//REGION AUTO_SNEAK_PARTY

//Auto-sneak character if user ID is the same as aura host
//This should only link chained characters in multiplayer, if they're under control of the host
IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_STEALTHY", (CHARACTERGUID)_AuraHost)
AND
DB_CA_Settings_AutoSneakParty(_Player, 1)
AND
HasActiveStatus(_AuraHost, "SNEAKING", 1)
AND
HasActiveStatus(_Player, "SNEAKING", 0)
AND
CharacterGetReservedUserID(_AuraHost,_ID)
AND
CharacterGetReservedUserID(_Player,_ID)
THEN
ApplyStatus(_Player, "SNEAKING", -1.0);
CharacterStatusText(_Player, "Auto-Sneaking");
//END_REGION

//REGION DEBUG
/*
IF
GameEventSet("GAMEEVENT_GameStarted")
AND
DB_IsPlayer(_Player)
AND
HasActiveStatus(_Player, "CA_STEALTHY", 1)
AND
DB_IsPlayer(_OtherPlayer)
AND
_OtherPlayer != _Player
AND
HasActiveStatus(_OtherPlayer, "CA_STEALTHY", 0)
AND
HasActiveStatus(_OtherPlayer, "CA_STEALTH_AURA", 0)
THEN
RemoveStatus(_Player, "CA_STEALTHY");
*/
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "_CivilAuras"
