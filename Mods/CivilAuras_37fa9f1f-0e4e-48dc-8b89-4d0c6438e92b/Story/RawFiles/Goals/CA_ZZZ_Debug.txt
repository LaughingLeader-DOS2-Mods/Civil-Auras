Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_CA_TestDummy((CHARACTERGUID)S_Player_TestGareth_000_ff106095-41b6-419e-9663-db1cac2e39bd);
//DB_CA_TestDummy((CHARACTERGUID)S_Player_TestBeast_000_977b5ea9-86ce-48d4-a36e-7cbeaf15a422);
//DB_CA_TestDummy((CHARACTERGUID)S_Player_TestPlayer_ab423237-5f34-48b9-be48-11fc9e32a9c6);

//DB_Debug_PreviousEquipment(_Player, _Slot, _Item);

//REGION Equipment Slots
DB_Debug_Slots("Weapon");
DB_Debug_Slots("Shield");
DB_Debug_Slots("Helmet");
DB_Debug_Slots("Breast");
DB_Debug_Slots("Leggings");
DB_Debug_Slots("Ring");
DB_Debug_Slots("Belt");
DB_Debug_Slots("Boots");
DB_Debug_Slots("Gloves");
DB_Debug_Slots("Amulet");
DB_Debug_Slots("Ring2");
//DB_Debug_Slots("Wings");
//DB_Debug_Slots("Horns");
//DB_Debug_Slots("Overhead");
//END_REGION
KBSECTION
//REGION FLAGS
PROC
CA_Settings_Debug_TraceFlag((CHARACTERGUID)_Player, (STRING)_FlagName, (INTEGER)_FlagValue)
AND
IntegertoString(_FlagValue, _FlagVal)
AND
StringConcatenate(_FlagName, " = ", _Part1)
AND
StringConcatenate(_Part1, _FlagVal, _Message)
THEN
CharacterStatusText(_Player, _Message);

IF
ObjectFlagSet(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
GlobalGetFlag("CA_DEBUG_DISPLAYFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, _Flag, _Val);

IF
ObjectFlagCleared(_Flag, (CHARACTERGUID)_Player, _Instance)
AND
GlobalGetFlag("CA_DEBUG_DISPLAYFLAGS", 1)
AND
ObjectGetFlag(_Player, _Flag, _Val)
THEN
CA_Settings_Debug_TraceFlag(_Player, _Flag, _Val);

IF
GlobalFlagSet(_Flag)
AND
GlobalGetFlag("CA_DEBUG_DISPLAYFLAGS", 1)
AND
GlobalGetFlag(_Flag, _Val)
AND
DB_IsPlayer(_Player)
THEN
CA_Settings_Debug_TraceFlag(_Player, _Flag, _Val);

IF
GlobalFlagCleared(_Flag)
AND
GlobalGetFlag("CA_DEBUG_DISPLAYFLAGS", 1)
AND
GlobalGetFlag(_Flag, _Val)
AND
DB_IsPlayer(_Player)
THEN
CA_Settings_Debug_TraceFlag(_Player, _Flag, _Val);


//END_REGION

//REGION AURA_OFF
PROC
CA_Debug_TurnOffAuras()
AND
DB_IsPlayer(_Player)
THEN
CA_Luck_TurnAuraOff(_Player);
CA_Stealth_TurnAuraOff(_Player);
CA_RemoveMotivated(_Player);
SetStoryEvent(_Player, "Event_CA_Bartering_TradeEnded");
CharacterStatusText(_Player, "[DEBUG] Disabling auras before removing bonus points.");

//END_REGION

//REGION BONUS POINTS
PROC
CA_Debug_ClearSavedEquipment((CHARACTERGUID)_Player)
AND
DB_Debug_PreviousEquipment(_Player, _Slot, _Item)
THEN
NOT DB_Debug_PreviousEquipment(_Player, _Slot, _Item);

PROC
CA_Debug_UnequipAll((CHARACTERGUID)_Player)
AND
DB_Debug_Slots(_Slot)
AND
CharacterGetEquippedItem(_Player, _Slot, (ITEMGUID)_Item)
THEN
CharacterUnequipItem(_Player, _Item);
DB_Debug_PreviousEquipment(_Player, _Slot, _Item);

PROC
CA_Debug_EquipAllPrevious((CHARACTERGUID)_Player)
AND
DB_Debug_PreviousEquipment(_Player, _Slot, _Item)
AND
ItemIsInCharacterInventory(_Item, _Player, 1)
THEN
CharacterEquipItem(_Player, _Item);
NOT DB_Debug_PreviousEquipment(_Player, _Slot, _Item);


PROC
CA_Debug_RemoveBonusPoints((CHARACTERGUID)_Player, (STRING)_StatName)
AND
CharacterGetBaseAbility(_Player, _StatName, _BaseValue)
AND
_BaseValue > 0
AND
CharacterGetAbility(_Player, _StatName, _BonusValue)
AND
IntegerSubtract(_BonusValue, _BonusValue, _Extra)
AND
IntegertoString(_BonusValue, _ExtraStr)
AND
StringConcatenate("[DEBUG] Removed ", _ExtraStr, _Str1)
AND
StringConcatenate(_Str1, "points from ", _Str2)
AND
StringConcatenate(_Str2, _StatName, _Message)
THEN
CharacterRemoveAbility(_Player, _StatName, _Extra);
CharacterStatusText(_Player, _Message);

IF
ObjectFlagSet("CA_DEBUG_REMOVEBONUSPOINTS", (CHARACTERGUID)_Player, _Instance)
AND
GetUUID(_Player, _Name)
AND
StringConcatenate("CA_Timer_Debug_RemovePoints_", _Name, _TimerName)
THEN
CA_Debug_TurnOffAuras();
CA_Debug_ClearSavedEquipment(_Player);
CA_Debug_UnequipAll(_Player);
TimerLaunch(_TimerName, 1000);
DB_Debug_Timers(_Player, _TimerName);

IF
TimerFinished(_TimerName)
AND
DB_Debug_Timers(_Player, _TimerName)
THEN
CA_Debug_RemoveBonusPoints(_Player, "Luck");
CA_Debug_RemoveBonusPoints(_Player, "Sneaking");
CA_Debug_RemoveBonusPoints(_Player, "Persuasion");
CA_Debug_RemoveBonusPoints(_Player, "Barter");
CharacterStatusText(_Player, "[DEBUG] Bonus points removal complete.");
NOT DB_Debug_Timers(_Player, _TimerName);
ObjectClearFlag(_Player, "CA_DEBUG_REMOVEBONUSPOINTS");
CA_Debug_EquipAllPrevious(_Player);
//END_REGION

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_CA_Settings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
CharacterGetAbility(_Player, "Luck", _BonusVal)
AND
CharacterGetBaseAbility(_Player, "Luck", _BaseVal)
AND
IntegertoString(_BonusVal, _BonusStr)
AND
IntegertoString(_BaseVal, _BaseStr)
AND
StringConcatenate("Base[", _BaseStr, _Str1)
AND
StringConcatenate(_Str1, "] Bonus?[", _Str2)
AND
StringConcatenate(_Str2, _BonusStr, _Str3)
AND
StringConcatenate(_Str3, "]", _Message)
THEN
CharacterStatusText(_Player, _Message);

/*

IF
RegionStarted("TestLevel")
AND
NOT DB_CA_ModTraders(CHARACTERGUID_Trailer_Humans_Female_Lohse_000_96482956-8428-401a-ab3f-4f5c76963401)
THEN
DB_CA_ModTraders(CHARACTERGUID_Trailer_Humans_Female_Lohse_000_96482956-8428-401a-ab3f-4f5c76963401);


IF
RegionStarted("TestLevel")
AND
DB_CA_TestDummy(_Org)
THEN
CharacterMakePlayer(_Org, NULL_00000000-0000-0000-0000-000000000000);
ProcAddDebugCharToParty(_Org);
DB_IsPlayer(_Org);
SetTag(_Org,"AVATAR");
GoalCompleted;

*/

/*
IF
SavegameLoaded(_,_,_,_)
AND
DB_IsPlayer(_Player)
THEN
ItemTemplateAddTo("MISC_CA_CatSack_Empty_b67a1a66-718f-4136-afc8-81ad5e64b9f9", _Player, 1);
ItemTemplateAddTo("MISC_CA_CatBait_90df95ee-2da2-4896-a6c6-abc8def42bc9", _Player, 1);
ItemTemplateAddTo("CON_CA_Potion_Talent_06ebb17f-babe-429c-91f3-11d537dad6ca", _Player, 2);
ItemTemplateAddTo("CON_CA_Potion_Ability_a3a2b79b-286a-4dda-8e6b-68c81b95ea63", _Player, 5);
ItemTemplateAddTo("CON_CA_Potion_Attribute_ca660d0e-05b6-4e43-a612-7f22e9fd2205", _Player, 5);
GoalCompleted;
*/

IF
GameEventSet("GAMEEVENT_GameStarted")
THEN
GoalCompleted;
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
