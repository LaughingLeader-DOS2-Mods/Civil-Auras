Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_CA_PersuasionBonus((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
KBSECTION
//REGION PROCS
PROC
CA_ClearPersuasionBonus((CHARACTERGUID)_Player)
AND
DB_CA_PersuasionBonus(_Player, _Val)
THEN
NOT DB_CA_PersuasionBonus(_Player, _Val);

PROC
CA_RemoveMotivated((CHARACTERGUID)_Player)
AND
HasActiveStatus(_Player, "CA_MOTIVATED", 1)
THEN
RemoveStatus(_Player, "CA_MOTIVATED");

PROC
CA_Persuasion_PlayAuraHostEffect((CHARACTERGUID)_Player)
AND
GetPosition(_Player, _X, _PY, _Z)
AND
RealSum(_PY, 2.0, _Y)
THEN
//CA_FX_Motivated_Impact_01:Dummy_OverheadFX:cast
PlayEffectAtPosition("CA_FX_Motivated_Impact_01", _X, _Y, _Z);
PlaySound(_Player, "Items_Inventory_Consumeables_Magic");

PROC
CA_Persuasion_PlayAuraEffect((CHARACTERGUID)_Player)
AND
GetPosition(_Player, _X, _PY, _Z)
AND
RealSum(_PY, 2.0, _Y)
THEN
PlayEffectAtPosition("CA_FX_Motivated_Impact_01", _X, _Y, _Z);

//END_REGION

/*
IF
CharacterUsedSkillOnTarget(_Caster, (CHARACTERGUID)_Target, "Target_CA_PepTalk",_)
AND
HasActiveStatus(_Target, "CA_MOTIVATED", 0)
AND
CharacterGetDisplayName(_Target, _Handle, _Ref)
AND
StringConcatenate(_Ref, ", I believe in you!", _Message)
THEN
DisplayText(_Caster, _Message);
*/

//REGION SKILL_USE
IF
CharacterUsedSkillOnTarget(_Caster, (CHARACTERGUID)_Target, "Target_CA_PepTalk",_)
AND
HasActiveStatus(_Target, "CA_PERSUASIVE", 1)
THEN
CharacterStatusText(_Caster, "Target is already Persuasive");

//REGION SKILL_USE
IF
CharacterUsedSkillOnTarget(_Caster, (CHARACTERGUID)_Target, "Target_CA_PepTalk",_)
AND
HasActiveStatus(_Target, "CA_PERSUASION_AURA", 1)
THEN
CharacterStatusText(_Caster, "Target is too charismatic");

IF
CharacterUsedSkillOnTarget(_Caster, (CHARACTERGUID)_Target, "Target_CA_PepTalk",_)
AND
HasActiveStatus(_Target, "CA_PERSUASIVE", 0)
AND
HasActiveStatus(_Target, "CA_PERSUASION_AURA", 0)
AND
NOT CA_Global_AbilityHigher(_Caster, _Target, "Persuasion")
AND
CharacterGetAbility(_Caster, "Persuasion", _CastVal)
AND
CharacterGetAbility(_Target, "Persuasion", _TargetVal)
AND
IntegertoString(_CastVal, _CastStr)
AND
IntegertoString(_TargetVal, _TargetStr)
THEN
CharacterStatusText(_Caster, "Persuasion not high enough to motivate target");
CA_Debug_Record(_Caster, "Persuasion is too low: Caster[", _CastStr, "] Target[", _TargetStr, "]");

IF
CharacterUsedSkillOnTarget(_Caster, (CHARACTERGUID)_Target, "Target_CA_PepTalk",_)
AND
HasActiveStatus(_Target, "CA_PERSUASIVE", 0)
AND
HasActiveStatus(_Target, "CA_PERSUASION_AURA", 0)
AND
CA_Global_AbilityHigher(_Caster, _Target, "Persuasion")
THEN
PlaySound(_Target, "Items_Inventory_Consumeables_Magic");
ApplyStatus(_Target, "CA_MOTIVATED", 30.0, 1);
CA_Aura_ApplyAuraBonus(_Caster, _Target, "Persuasion", "CA_MOTIVATED");

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_PersuasionToggle", _)
AND
HasAppliedStatus(_Player, "CA_PERSUASION_AURA", 0)
THEN
CA_Persuasion_PlayAuraHostEffect(_Player);
ApplyStatus(_Player, "CA_PERSUASION_AURA", -1.0, 1);
CA_Aura_StoreCurrentAbilityValue(_Player, "Persuasion");

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_PersuasionToggle", _)
AND
HasActiveStatus(_Player, "CA_PERSUASION_AURA", 1)
THEN
RemoveStatus(_Player, "CA_PERSUASION_AURA");
CharacterStatusText(_Player, "Persuasion Aura Disabled");

//END_REGION

/*
IF
DialogEnded((STRING)_Dialog, (INTEGER)_ID)
AND
DialogGetInvolvedPlayer(_ID, 1, (CHARACTERGUID)_Player)
AND
HasActiveStatus(_Player, "CA_MOTIVATED", 1)
THEN
RemoveStatus(_Player, "CA_MOTIVATED");
*/

//REGION PARTY_BONUSES
IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_MOTIVATED", (CHARACTERGUID)_AuraHost)
AND
DB_IsPlayer(_Player)
THEN
CA_Persuasion_PlayAuraEffect(_Player);

IF
CharacterStatusRemoved((CHARACTERGUID)_Player, "CA_MOTIVATED",_)
AND
DB_IsPlayer(_Player)
THEN
CA_Aura_RemoveAuraBonus(_Player, "Persuasion", "CA_MOTIVATED");

IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_PERSUASIVE", (CHARACTERGUID)_AuraHost)
AND
DB_IsPlayer(_Player)
THEN
CA_RemoveMotivated(_Player);
CA_Persuasion_PlayAuraEffect(_Player);
CA_Aura_ApplyAuraBonus(_AuraHost, _Player, "Persuasion", "CA_PERSUASIVE");

IF
CharacterStatusRemoved((CHARACTERGUID)_Player, "CA_PERSUASIVE",_)
AND
DB_IsPlayer(_Player)
THEN
CA_Aura_RemoveAuraBonus(_Player, "Persuasion", "CA_PERSUASIVE");
//END_REGION

//REGION LEGACY_REMOVAL
PROC
CA_Legacy_RemoveOldAuras((CHARACTERGUID)_Player)
AND
DB_CA_PersuasionBonus(_Player, _Val)
THEN
CA_Legacy_RemoveOldBonus(_Player, "Persuasion", _Val);
NOT DB_CA_PersuasionBonus(_Player, _Val);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
