Version 1
SubGoalCombiner SGC_AND
INITSECTION
//DB_CA_LuckBonus((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
//DB_CA_LuckAuraActive((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
//DB_CA_Temp_CheckPlayer((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000);

//Proc Cheat Sheet
//CA_ClearLuckActive
//CA_ClearLuckBonus
KBSECTION
//REGION DEBUG
/*
IF
CharacterStatusApplied(S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
CharacterGetAbility(S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, "Luck", _PlayerLuck)
AND
CharacterGetBaseAbility(_AuraHost, "Luck", _TargetLuck)
AND
IntegerSubtract(_TargetLuck, _PlayerLuck, _Bonus)
AND
IntegertoString(_PlayerLuck, _LuckStr)
AND
IntegertoString(_TargetLuck, _TargetLuckStr)
AND
IntegertoString(_Bonus, _BonusLuckStr)
AND
StringConcatenate("Base: ", _LuckStr, _Out1)
AND
StringConcatenate(" Target: ", _TargetLuckStr, _Out2)
AND
StringConcatenate(" Bonus: ", _BonusLuckStr, _Out3)
AND
StringConcatenate(_Out1, _Out2, _PreFinalMessage1)
AND
StringConcatenate(_PreFinalMessage1, _Out3, _FinalMessage)
THEN
DisplayText(S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, _FinalMessage);
DebugText(S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, _FinalMessage);
//CharacterAddAbility(S_Player_Beast_f25ca124-a4d2-427b-af62-df66df41a978, "Luck", _Bonus);
*/

/*
IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
DB_IsPlayer(_Player)
THEN
CharacterStatusText(_Player, "Player");

IF
CharacterStatusRemoved((CHARACTERGUID)_Player, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
NOT DB_IsPlayer(_Player)
THEN
CharacterStatusText(_Player, "Not Player");

IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
GetUUID(_Player, _Name)
AND
StringConcatenate("Player: ", _Name, _Message)
THEN
DB_NOOP(1);
//DisplayText(_Player, _Message);
*/
//END_REGION

//REGION PROCS
PROC
CA_ClearLuckActive((CHARACTERGUID)_Player)
AND
DB_CA_LuckAuraActive(_Player, _Val)
THEN
NOT DB_CA_LuckAuraActive(_Player, _Val);

PROC
CA_ClearLuckBonus((CHARACTERGUID)_Player)
AND
DB_CA_LuckBonus(_Player, _Val)
THEN
NOT DB_CA_LuckBonus(_Player, _Val);

PROC
CA_LuckyAuraEffect((CHARACTERGUID)_Player)
AND
GetPosition(_Player, _X, _PY, _Z)
AND
RealSum(_PY, 1.0, _Y)
THEN
PlayEffectAtPosition("CA_FX_LuckyAura_Impact_01", _X, _Y, _Z);
PlaySound(_Player, "Skill_Summon_Cast_AoE_Divine_Aurora");

PROC
CA_LuckyEffect((CHARACTERGUID)_Player)
AND
GetPosition(_Player, _X, _PY, _Z)
AND
RealSum(_PY, 1.0, _Y)
THEN
PlayEffectAtPosition("CA_FX_Lucky_Impact_01", _X, _Y, _Z);
PlaySound(_Player, "Proj_Arrow_Charming_Projectile");

//END_REGION

//REGION SKILL_USE
IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_LuckyToggle", _)
AND
HasAppliedStatus(_Player, "CA_LUCKY_AURA", 0)
THEN
ApplyStatus(_Player, "CA_LUCKY_AURA", -1.0);
CA_ClearLuckActive(_Player);
DB_CA_LuckAuraActive(_Player, 1);
CharacterStatusText(_Player, "Aura turned on.");
CA_LuckyAuraEffect(_Player);

IF
CharacterUsedSkill((CHARACTERGUID)_Player, "Shout_CA_LuckyToggle", _)
AND
HasActiveStatus(_Player, "CA_LUCKY_AURA", 1)
THEN
RemoveStatus(_Player, "CA_LUCKY_AURA");
CA_ClearLuckActive(_Player);
CharacterStatusText(_Player, "Aura turned off.");

//END_REGION

//REGION AUTO-TOGGLE
IF
ObjectEnteredCombat((CHARACTERGUID)_Player, _)
AND
HasActiveStatus(_Player, "CA_LUCKY_AURA", 1)
THEN
CA_ClearLuckActive(_Player);
DB_CA_LuckAuraActive(_Player, 1);
RemoveStatus(_Player, "CA_LUCKY_AURA");

IF
ObjectLeftCombat((CHARACTERGUID)_Player, _)
AND
DB_CA_LuckAuraActive(_Player, 1)
AND
HasActiveStatus(_Player, "CA_LUCKY_AURA", 0)
THEN
ApplyStatus(_Player, "CA_LUCKY_AURA", -1.0);
CA_LuckyAuraEffect(_Player);

IF
SavegameLoaded(_,_,_,_)
AND
DB_IsPlayer(_Player)
AND
DB_CA_LuckAuraActive(_Player, 1)
THEN
ApplyStatus(_Player, "CA_LUCKY_AURA", -1.0);
CA_LuckyAuraEffect(_Player);
//END_REGION

//REGION PARTY_BONUSES
IF
CharacterStatusApplied((CHARACTERGUID)_Player, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
DB_IsPlayer(_Player)
AND
CharacterGetAbility(_Player, "Luck", _PlayerLuck)
AND
CharacterGetBaseAbility(_AuraHost, "Luck", _TargetLuck)
AND
_PlayerLuck < _TargetLuck
AND
IntegerSubtract(_TargetLuck, _PlayerLuck, _Bonus)
THEN
CA_ClearLuckBonus(_Player);
DB_CA_LuckBonus(_Player, _Bonus);
CharacterAddAbility(_Player, "Luck", _Bonus);
CA_LuckyEffect(_Player);

IF
CharacterStatusRemoved((CHARACTERGUID)_Player, "CA_LUCKY", (CHARACTERGUID)_AuraHost)
AND
DB_CA_LuckBonus(_Player, _Bonus)
AND
_Bonus > 0
THEN
CharacterRemoveAbility(_Player, "Luck", _Bonus);
CA_ClearLuckBonus(_Player);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "_CivilAuras"
