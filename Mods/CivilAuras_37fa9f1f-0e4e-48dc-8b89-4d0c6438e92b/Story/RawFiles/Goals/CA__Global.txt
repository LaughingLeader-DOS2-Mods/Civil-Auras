Version 1
SubGoalCombiner SGC_AND
INITSECTION
//Settings
DB_CA_Settings_AutoSneakParty((CHARACTERGUID)NULL_00000000-0000-0000-0000-000000000000, 0);
DB_CA_Settings_BarterMessages(1, 50); //On, Random Chance
//DB_CA_ModVersion("1.4.0");
KBSECTION
//REGION HELPER_METHODS
PROC
CA_PrintMessage((CHARACTERGUID)_Target, (STRING)_Prefix, (STRING)_Var, (STRING)_Suffix)
AND
StringConcatenate(_Prefix, _Var, _Str1)
AND
StringConcatenate(_Str1, _Suffix, _Str2)
THEN
CharacterStatusText(_Target, _Str2);

PROC
CA_PrintInt((CHARACTERGUID)_Target, (STRING)_Prefix, (INTEGER)_VarInt, (STRING)_Suffix)
AND
IntegertoString(_VarInt, _Var)
AND
StringConcatenate(_Prefix, _Var, _Str1)
AND
StringConcatenate(_Str1, _Suffix, _Str2)
THEN
CharacterStatusText(_Target, _Str2);

//END_REGION

//REGION SETTINGS
PROC
CA_ClearSettings_AutoSneak((CHARACTERGUID)_Player)
AND
DB_CA_Settings_AutoSneakParty(_Player, _Val)
THEN
NOT DB_CA_Settings_AutoSneakParty(_Player, _Val);

IF
CharacterUsedItemTemplate((CHARACTERGUID)_Player, "BOOK_Book_CivilAuraSettings_df08089b-1a04-47a5-9294-7103f9e3b7a2", (ITEMGUID)_Item)
AND
//ObjectGetDialogFlag(_Player, "CA_Settings_Open", 0)
CharacterIsInCombat(_Player,0)
THEN
//Proc_StartDialog((INTEGER)_Automated,(STRING)_Dialog,(GUIDSTRING)_Speaker1,(GUIDSTRING)_Speaker2)
Proc_StartDialog(0, "CA_Dialog_Config", _Player);
//PROC_GLOBAL_DialogStartRequested(_Player, _Item);
//ObjectSetDialogFlag(_Player, "CA_Settings_Open", 1);

IF
ObjectFlagSet("CA_AUTOSNEAK_ON", (CHARACTERGUID)_Player, _Instance)
//ObjectWasTagged((ITEMGUID)_Item, "CA_CONFIG_AUTOSNEAK")
//AND
//ItemGetOwner(_Item, _Player)
AND
DB_IsPlayer(_Player)
THEN
CA_ClearSettings_AutoSneak(_Player);
DB_CA_Settings_AutoSneakParty(_Player, 1);
ObjectClearFlag(_Player, "CA_AUTOSNEAK_OFF");
CharacterStatusText(_Player, "Auto-Party Sneak On");

IF
ObjectFlagSet("CA_AUTOSNEAK_OFF", (CHARACTERGUID)_Player, _Instance)
AND
DB_IsPlayer(_Player)
THEN
CA_ClearSettings_AutoSneak(_Player);
ObjectClearFlag(_Player, "CA_AUTOSNEAK_ON");
CharacterStatusText(_Player, "Auto-Party Sneak Off");

//END_REGION

//REGION BONUS_POINTS
IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddTalentPoint")
THEN
CharacterAddTalentPoint(_Player,1);

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddAbilityPoint")
THEN
CharacterAddAbilityPoint(_Player, 1);

/*
IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddCivilPoint")
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
*/

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddAttributePoint")
THEN
CharacterAddAttributePoint(_Player, 1);

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddSourcePoint")
THEN
CharacterAddSourcePoints(_Player, 1);
//END_REGION

//REGION DATABASE_REFRESH
PROC
CA_Global_ResetDatabases()
AND
DB_CA_ModVersion(_Version)
THEN
NOT DB_CA_ModVersion(_Version);

//Add the mod version to fresh saves, to avoid rebuilding.
IF
RegionStarted("TUT_Tutorial_A")
AND
NOT DB_CA_ModVersion("1.4.0")
THEN
DB_CA_ModVersion("1.4.0");

//For refreshing existing saves
IF
SavegameLoaded(_,_,_,_)
AND
NOT DB_CA_ModVersion("1.4.0")
AND
DB_IsPlayer(_Player)
AND
NOT DB_CA_Refreshing(1)
THEN
CA_Global_ResetDatabases();
DB_CA_ModVersion("1.4.0");
DB_CA_Refreshing(1);
SetStoryEvent(_Player, "CA_Refresh");

IF
StoryEvent(_Object, "CA_Refresh")
AND
DB_CA_Refreshing(_Val)
THEN
NOT DB_CA_Refreshing(_Val);

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
