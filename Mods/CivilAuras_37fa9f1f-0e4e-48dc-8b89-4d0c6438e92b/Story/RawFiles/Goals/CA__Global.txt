Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_CA_ModVersion("1.7.1");
KBSECTION
//REGION HELPER_METHODS
PROC
CA_PrintMessage((CHARACTERGUID)_Target, (STRING)_Prefix, (STRING)_Var, (STRING)_Suffix)
AND
StringConcatenate(_Prefix, _Var, _Str1)
AND
StringConcatenate(_Str1, _Suffix, _Str2)
THEN
CharacterStatusText(_Target, _Str2);

PROC
CA_PrintInt((CHARACTERGUID)_Target, (STRING)_Prefix, (INTEGER)_VarInt, (STRING)_Suffix)
AND
IntegertoString(_VarInt, _Var)
AND
StringConcatenate(_Prefix, _Var, _Str1)
AND
StringConcatenate(_Str1, _Suffix, _Str2)
THEN
CharacterStatusText(_Target, _Str2);

QRY
CA_Global_UserMatch((CHARACTERGUID)_Player, (CHARACTERGUID)_Character)
AND
CharacterGetReservedUserID(_Player,_PlayerID)
AND
CharacterGetReservedUserID(_Character, _CharID)
AND
_CharID == _PlayerID
THEN
DB_NOOP(1);

QRY
CA_Global_AbilityHigher((CHARACTERGUID)_Player, (CHARACTERGUID)_Target, (STRING)_Ability)
AND
CharacterGetAbility(_Player, _Ability, _Val1)
AND
CharacterGetAbility(_Target, _Ability, _Val2)
AND
_Val1 > _Val2
THEN
DB_NOOP(1);
//END_REGION

//REGION BONUS_POINTS
IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddTalentPoint")
THEN
CharacterAddTalentPoint(_Player,1);

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddAbilityPoint")
THEN
CharacterAddAbilityPoint(_Player, 1);

/*
IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddCivilPoint")
THEN
CharacterAddCivilAbilityPoint(_Player, 1);
*/

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddAttributePoint")
THEN
CharacterAddAttributePoint(_Player, 1);

IF
StoryEvent((CHARACTERGUID)_Player, "Event_CA_AddSourcePoint")
THEN
CharacterAddSourcePoints(_Player, 1);
//END_REGION

//REGION DATABASE_REFRESH
/*
PROC
CA_Global_BuildSettings()
AND
DB_IsPlayer(_Player)
AND
NOT DB_CA_Settings_Barter_Messages(_Player,_,_,_)
THEN
DB_CA_Settings_Barter_Messages(_Player, 1, 100, 75);

//Add the mod version to fresh Origin saves, to avoid rebuilding.
IF
GameStarted("TUT_Tutorial_A", _Mode)
AND
DB_CA_ModVersion("1.7.1")
THEN
DB_CA_ModVersion("1.7.1");
CA_Global_BuildSettings();
*/

QRY
CA_QRY_Global_RefreshNeeded()
AND
NOT DB_CA_ModVersion(_)
THEN
DB_NOOP(1);

QRY
CA_QRY_Global_RefreshNeeded()
AND
DB_CA_ModVersion(_Version)
AND
_Version != "1.7.1"
THEN
DB_NOOP(1);

//For refreshing existing saves
IF
SavegameLoaded(_,_,_,_)
AND
CA_QRY_Global_RefreshNeeded()
AND
DB_IsPlayer(_Player)
AND
NOT DB_CA_Refreshing(1)
THEN
SysClear("DB_CA_ModVersion", 1);
DB_CA_ModVersion("1.7.1");
DB_CA_Refreshing(1);
SetStoryEvent(_Player, "CA_Refresh");
CA_Debug_Record(_Player, "Mod version changed. Updating to new version.");

PROC
CA_Legacy_Start_RemoveOldAuras()
AND
DB_IsPlayer(_Player)
THEN
CA_Debug_Record(_Player, "[Legacy] Removed old aura data");
CA_Legacy_RemoveOldAuras(_Player);

IF
StoryEvent(_Object, "CA_Refresh")
AND
DB_CA_Refreshing(_Val)
THEN
SysClear("DB_CA_Refreshing", 1);
CA_Legacy_Start_RemoveOldAuras();

//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ZZZ_CivilAuras"
